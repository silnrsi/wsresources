c      Senegalese Keyboard Control File for Tavultesoft Keyman
c      5.0
c
c
c	For use with SIL-Dakar fonts (orthography for Senegal languages)
c
c  Version 1.0 by Knox Haggie
c  
c  Modification history
c  1.1	Use '/' instead of apostrophe which is often an alphabetic
c		character
c  1.2	Keep accented characters in stores for easier modification.
c		And added accent aigu on its own.
c  1.3      Added 'glottal' character.  Swapped use of double quotes to
c           type diaresis to use semi-colon instead.
c  1.4      Updated for revised Senegal fonts.
c           * Includes Kwatay and Kasa characters.
c           * Literacy 'a' removed - use "literacy" font instead.
c           Also, added keystrokes for s-hacek and various accents.
c  1.5      Modified for Dakar fonts instead of Sen fonts.
c  1.6      Updated to add new characters (double-barred t) and diacritics
c           (under-bar, tilde).  Also added key combination for s-hacek
c           which is used in Budik.
c  1.7      Added Mankanya Characters

NAME "SIL Sénégal Dakar"
COPYRIGHT "2001 SIL Sénégal, La Gambie et Guinée Bissau"
BITMAP Dakar 
VERSION 5.0
MESSAGE "Pour tous les langues Sénégalaises. Clavier original"        

store(cons) 'bBcCdDgGpPtTyY'
store(implosive) d248 d216 d170 d186 d229 d197 d177 d172 d163 d165 d174 d162 d255 d159
store(vowel) 'aAeEiIoOuU'
store(vacute) d225 d193 d233 d201 d237 d205 d243 d211 d250 d218   
store(vgrave) d224 d192 d232 d200 d236 d204 d242 d210 d249 d217
store(vcircon) d226 d194 d234 d202 d238 d206 d244 d212 d251 d219 
store(vumlaut) d228 d196 d235 d203 d239 d207 d246 d214 d252 d220
store(small) 'acemnopqrsuvwxyz'
store(tall) 'bdfhijkltABCDEFGHIJKLMNOPQRSTUVWXYZ'
store(other) '!"#$%&()*+,-./0123456789:;<=>?@[\]^_`{|}' d127

begin >  use(Main)

group(Main) using keys

c Implosives:

"," + any(cons) > index(implosive, 2)

c Nasals

"," + "n" > d230		c sm eng
"," + "N" > d198		c cap eng
";" + "n" > d153		c sm enye
";" + "N" > d181		c cap enye

c Tildes
"~" + "n" > d241		c sm n tilde
"~" + "N" > d209		c cap n tilde
"~" + "a" > d227
"~" + "A" > d195

c other letters can have tilde in combination with separate diacritic
"~" + any(small) > index(small, 2) d152
"~" + any(tall) > index(tall, 2) d175
"~" + " " > "~"
"~" + "~" > "~"
"~" + any(other) > '~' index(other, 2)

c accents on their own:
"=" + "/" > d180			c acute

c vowels with accent aigu:
"/" + any(vowel) > index(vacute, 2)

c vowels with accent grave:
"`" + any(vowel) > index(vgrave, 2)
"`" + "n" > d130
"`" + "N" > d132

c vowels with accent circonflexe:
"^" + any(vowel) > index(vcircon, 2)

c vowels with umlaut:
';' + any(vowel) > index(vumlaut, 2)

c open vowels
"=" + "e" > d141
"=" + "E" > d142
"=" + "o" > d143
"=" + "O" > d144

C Jola Kasa
"," + "l" > d161
"," + "L" > d223

c Digraphs
"," + "o" >  d156		c oe
"," + "O" >  d140		c OE

c French:
"/" + "c" >  d231
"/" + "C" >  d199
"<" + "<" > d171
">" + ">" > d187

c Balanta:
"/" + "t" > d240			c barred t
"/" + "T" > d208			c barred T

c Mankanya
";" + "s" > d134
";" + "S" > d135
";" + "t" > d168
";" + "T" > d169

c Budik:
"^" + "s" > d154			c s-hacek
"^" + "S" > d138			c S-hacek

c Glottal character
";" + ";" > d191
